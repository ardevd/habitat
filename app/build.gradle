apply plugin: 'com.android.application'
apply plugin: 'org.sonarqube'
apply plugin: 'com.github.ksoichiro.console.reporter'
apply plugin: 'jacoco-android'


android {
    signingConfigs {
    }
    compileSdkVersion 27
    buildToolsVersion "27.0.3"
    defaultConfig {
        applicationId "no.aegisdynamics.habitat"
        minSdkVersion 23
        targetSdkVersion 27
        versionCode 44
        versionName project.habitatVersionName
        vectorDrawables.useSupportLibrary = true
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }
    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    externalNativeBuild {
        cmake {
            path "CMakeLists.txt"
        }
    }

    testOptions {
        unitTests.all {
            jacoco {
                includeNoLocationClasses = true
            }
        }
    }
}

apply plugin: 'kotlin-android'

dependencies {
    compile fileTree(include: ['*.jar'], dir: 'libs')
    androidTestImplementation('com.android.support.test.espresso:espresso-core:2.2.2', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })
    androidTestImplementation('com.android.support.test.espresso:espresso-contrib:2.2.2', {
        exclude group: 'com.android.support', module: 'support-annotations'
        exclude group: 'com.android.support', module: 'support-v4'
        exclude group: 'com.android.support', module: 'recyclerview-v7'
        exclude group: 'com.android.support', module: 'design'
    })
    androidTestImplementation 'org.mockito:mockito-core:2.8.9'
    androidTestImplementation 'com.google.dexmaker:dexmaker:1.2'
    androidTestImplementation 'com.google.dexmaker:dexmaker-mockito:1.2'
    // Dependencies for local unit tests
    testImplementation 'junit:junit:4.12'
    testImplementation 'org.mockito:mockito-all:1.10.19'
    testImplementation 'org.json:json:20140107'

    // Google stuff
    implementation 'com.android.support:appcompat-v7:27.1.0'
    implementation 'com.android.support:design:27.1.0'
    implementation 'com.android.support:cardview-v7:27.1.0'
    implementation 'com.android.support:recyclerview-v7:27.1.0'
    implementation 'com.google.guava:guava:20.0'
    implementation 'com.github.bumptech.glide:glide:3.8.0'
    implementation 'com.android.volley:volley:1.1.0'
    implementation 'com.android.support:palette-v7:27.1.0'
    implementation 'com.android.support.constraint:constraint-layout:1.0.2'
    // Third party libraries
    implementation 'com.github.feeeei:CircleSeekbar:v1.1.2'
    implementation 'me.angrybyte.picker:picker:1.3.1'
    implementation 'com.diogobernardino:williamchart:2.5.0'
    implementation 'br.com.simplepass:loading-button-android:1.12.1'
    implementation 'uk.co.samuelwall:material-tap-target-prompt:2.7.0'
    implementation 'com.xw.repo:bubbleseekbar:3.14-lite'
    implementation 'com.github.medyo:android-about-page:1.2.2'
    // Firebase and GMS
    implementation 'com.google.firebase:firebase-messaging:12.0.1'
    implementation 'com.google.android.gms:play-services-base:12.0.1'
    implementation 'com.google.android.gms:play-services-places:12.0.1'
    apply plugin: 'com.google.gms.google-services'
}

consoleReporter {
    jacoco {
        reportFile = new File("${project.buildDir}/reports/jacoco/jacocoTestDebugUnitTestReport/jacocoTestDebugUnitTestReport.xml")
    }
}

jacocoAndroidUnitTestReport {

    excludes += ['**/*Activity.*',
                 '**/*Fragment.*',
                 '**/*DbHelper.*',
                 '**/*BackupNotifications.*',
                 '**/*GooglePlayServicesHelper.*',
                 '**/*KeyStoreHelper.*',
                 '**/*InputStreamVolleyRequest.*',
                 '**/*PaletteHelper.*',
                 '**/*UserCredentialsManager.*',
                 '**/adapters/**',
                 '**/base/**',
                 '**/dialogs/**',
                 '**/fcm/**',
                 '**/jobservices/**',
                 '**/participate/**',
                 '**/preferences/**',
                 '**/provider/**',
                 '**/splash/**',
                 '**/zautomation/**']
}

sonarqube {
    properties {
        property "sonar.projectName", "Habitat"
        property "sonar.projectKey", "android_habitat"
        property "sonar.sources","src/main/java"
        property "sonar.language","java"
        property "sonar.java.coveragePlugin", "jacoco"
        property "sonar.jacoco.reportPaths","${project.buildDir}/jacoco/testDebugUnitTest.exec"
    }
}

tasks.sonarqube.dependsOn jacocoTestReport
